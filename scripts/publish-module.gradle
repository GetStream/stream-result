apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'org.jetbrains.dokka'

if (project.plugins.findPlugin("com.android.library")) {
  android {
    publishing {
      singleVariant("release") {
        withJavadocJar()
        withSourcesJar()
      }
    }
  }
} else {
  java {
    withSourcesJar()
    withJavadocJar()
  }
}

tasks.withType(dokkaHtmlPartial.getClass()).configureEach {
  pluginsMapConfiguration.set(
    ["org.jetbrains.dokka.base.DokkaBase": """{ "separateInheritedMembers": true}"""]
  )
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
  publishing {
    publications {
      release(MavenPublication) {
        groupId PUBLISH_GROUP_ID
        artifactId PUBLISH_ARTIFACT_ID
        version PUBLISH_VERSION
        if (project.plugins.findPlugin("com.android.library")) {
          from components.release
        } else {
          from components.java
        }

        pom {
          name = PUBLISH_ARTIFACT_ID
          description = 'Result and error models for standardizing responses on Kotlin and Android.'
          url = 'https://github.com/getstream/stream-result'
          licenses {
            license {
              name = 'The Apache Software License, Version 2.0'
              url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
          }
          developers {
            developer {
              id = 'jcminarro'
              name = 'Jc Mi√±arro'
            }
            developer {
              id = 'skydoves'
              name = 'Jaewoong Eum'
            }
          }
          scm {
            connection = 'scm:git:github.com/getstream/stream-result.git'
            developerConnection = 'scm:git:ssh://github.com/getstream/stream-result.git'
            url = 'https://github.com/getstream/stream-result/tree/main'
          }
        }
      }
    }
  }
}

signing {
  useInMemoryPgpKeys(
    rootProject.ext["signing.keyId"],
    rootProject.ext["signing.key"],
    rootProject.ext["signing.password"],
  )
  sign publishing.publications
}
